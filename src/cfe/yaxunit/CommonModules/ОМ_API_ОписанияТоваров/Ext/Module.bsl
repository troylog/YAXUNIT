#Область СлужебныйПрограммныйИнтерфейс

Процедура ИсполняемыеСценарии() Экспорт
    
   ЮТТесты.ДобавитьТестовыйНабор("HTTP-сервисы ОписанияТоваров").ВТранзакции()
   		.ДобавитьТест("ПолучитьОписаниеТоваровПоАртикулу", "", "Позитив, Task-1, Каратаев Олег")
   		.ДобавитьТест("ПолучитьОписаниеТоваровПоАртикулуНегатив", "", "Негатив, Task-1, Каратаев Олег")
   		.ДобавитьТест("ПолучитьОписаниеТоваровПоАртикулуПозитивСоздание", "", "Позитив, Task-1, Каратаев Олег")
		;

КонецПроцедуры

Процедура ПолучитьОписаниеТоваровПоАртикулу() Экспорт
	
	// 1. Подготовка параметров и генерация данных для выполнения функции.
	Артикул = "Bos0009";
	
	// 2. Выполнение функции.
	Товар = API_ОписанияТоваров.ПолучитьТоварПоАртикулу(Артикул);
	
	// 3. Проверка результата.
	ЮТест.ОжидаетЧто(Товар)
		.ИмеетТип("СправочникОбъект.Товары")
		.Свойство("Артикул").Равно(Артикул)
		.Свойство("Наименование").Равно("Босоножки")
		;
		
КонецПроцедуры 
	
Процедура ПолучитьОписаниеТоваровПоАртикулуНегатив() Экспорт
	
	// 1. Подготовка параметров и генерация данных для выполнения функции.
	Артикул = "111111112222222222222333333333333";
	
	// 2. Выполнение функции.
	Товар = API_ОписанияТоваров.ПолучитьТоварПоАртикулу(Артикул);
	
	// 3. Проверка результата.
	ЮТест.ОжидаетЧто(Товар)
		.ИмеетТип("Неопределено")
		;
		
КонецПроцедуры 
	
Процедура ПолучитьОписаниеТоваровПоАртикулуПозитивСоздание() Экспорт
	
	// Описание программного создания: https://bia-technologies.github.io/yaxunit/docs/features/test-data/data-generation/
	
	Артикул = "Test1234";
	Наименование = "Test_Хлеб";
	
	// Создание товара
	НовыйТовар = ЮТест.Данные().КонструкторОбъекта("Справочники.Товары")
		.ФикцияОбязательныхПолей()
		.Установить("Артикул", Артикул) // проверочный артикул
		.Установить("Наименование", Наименование) // проверочное наименование
		.ФикцияРеквизитов("Поставщик, Вид, Штрихкод, Описание")
		.Записать()
		;
			
    Товар = API_ОписанияТоваров.ПолучитьТоварПоАртикулу(Артикул);
	
	ЮТест.ОжидаетЧто(Товар)
		.ИмеетТип("СправочникОбъект.Товары")
		.Свойство("Артикул").Равно(Артикул)
		.Свойство("Наименование").Равно(Наименование)
		;

КонецПроцедуры


#КонецОбласти

#Область СлужебныеПроцедурыИФункции


#КонецОбласти